.CODE32

.GLOBL LOCATE_RSDT
.EXTERN NL
.EXTERN PRINT
.EXTERN PRINT_N
.EXTERN PRINT_HEX_8
.EXTERN PRINT_HEX_16
.EXTERN PRINT_HEX_32

LOCATE_RSDT:
	MOV $0x0F, %CH
	MOV $0xE0000, %EBX

SEARCH_LOOP:
	// "RSD "
	CMPL $0x20445352, (%EBX)
	JNE .FALSE_FLAG
	// "PTR "
	CMPL $0x20525450, 4(%EBX)
	JE RSDP_FOUND

	.FALSE_FLAG:
	ADD $16, %EBX
	CMP $0xFFFFF, %EBX

	JG FAIL
	JMP SEARCH_LOOP

RSDP_FOUND:
	LEA FOUND, %ESI
	CALL PRINT
	MOV %EBX, %EAX
	CALL PRINT_HEX_32
	CALL NL
	XOR %EAX, %EAX
	XOR %DL, %DL
	MOV $20, %ECX

CHECKSUM_LOOP:
	MOV (%EBX), %DL
	ADD %DL, %AL
	INC %EBX
	LOOP CHECKSUM_LOOP

	OR %AL, %AL
	JNZ CHECKSUM_FAIL

	MOV $0x0F, %CH
	LEA OEMID, %ESI
	CALL PRINT
	SUB $11, %EBX
	MOV $6, %EDX
	MOV %EBX, %ESI
	CALL PRINT_N
	CALL NL

	ADD $7, %EBX
	LEA RSDT_ADDR, %ESI
	CALL PRINT
	MOV (%EBX), %EAX
	CALL PRINT_HEX_32
	CALL NL

	POP %EBX
	PUSH %EAX
	PUSH %EBX

	RET

CHECKSUM_FAIL:
	LEA CHECKSUM_INVALID, %ESI
	JMP FAILURE

FAIL:
	LEA NOT_FOUND, %ESI

FAILURE:
	MOV $0x0C, %CH
	CALL PRINT
	CALL NL

HANG:
	JMP HANG


.SECTION .rodata
FOUND: .ASCIZ "[ ACPI ] RSDP FOUND: 0x"
OEMID: .ASCIZ "[ ACPI ] OEMID: "
RSDT_ADDR: .ASCIZ "[ ACPI ] RSDT address: 0x"
NOT_FOUND: .ASCIZ "[ ACPI ] RSDP NOT FOUND!"
CHECKSUM_INVALID: .ASCIZ "[ ACPI ] RSDT CHECKSUM INVALID!"
